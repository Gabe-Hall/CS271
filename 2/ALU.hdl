// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zx.nx
    Mux16(a=x,b=false,sel=zx,out=mzx);
    Not16(in=mzx,out=nzx);
    Mux16(a=mzx,b=nzx,sel=nx,out=mnx);

    //zy.ny
    Mux16(a=y,b=false,sel=zy,out=mzy);
    Not16(in=mzy,out=nzy);
    Mux16(a=mzy,b=nzy,sel=ny,out=mny);

    Add16(a=mnx,b=mny,out=xaddy);
    And16(a=mnx,b=mny,out=xandy);

    //f.no
    Mux16(a=xandy,b=xaddy,sel=f,out=mf);
    Not16(in=mf,out=nf);
    Mux16(a=mf,b=nf,sel=no,out=mno);

    //zr
    And16(a=mno,b=mno,out[0..7]=out1,out[8..15]=out2);
    Or8Way(in=out1,out=out11);
    Or8Way(in=out2,out=out22);
    Or(a=out11,b=out22,out=out12);
    Not(in=out12,out=zr);

    //ng
    And16(a=mno,b=mno,out[15]=ng);

    //out
    And16(a=mno,b=mno,out=out);
}